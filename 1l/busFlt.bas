' ASPEN PowerScrip sample program
'
' BUSFLT.BAS
'
' Simulate 3PH and 1LN fault on the selected bus. 
' If none selected fault every bus in the network 
' Do simulation with single and double branch outages
' Record maximum fault current
'
' Output to a file:
'  - Fault current
'  - Max fault current and location
'
' Version 1.0
' Category: OneLiner
'
' PowerScript functions called:
'   GetEquipment()
'   NextBusByName()
'   GetBusEquipment()
'   FaultDescription()
'   DoFault()
'   ShowFault()
'   GetSCCurrent()
'
Sub main()
   ' Variable declaration
   Dim Imagnitude(16) As Double
   Dim Iangle(16) As Double
   Dim FltConn(4) As Long
   Dim FltOpt(14) As Double
   Dim OutageOpt(4) As Long
   Dim OutageLst(30) As Long
   If OpenOutFile() = 0 Then Stop

   'fault connections
   FltConn(1) = 1   ' Do 3PH
   FltConn(2) = 0
   FltConn(3) = 1   ' Do 1LG
   FltConn(4) = 0
   FltOpt(1)  = 1   ' Bus fault no outage
   FltOpt(2)  = 1  
   OutageOpt(1) = 1 ' With one outage at a time
   OutageOpt(2) = 1 ' With two outage at a time
   Rflt         = 0 ' Fault R
   Xflt         = 0 ' Fault X
   ClearPrev    = 1 ' Don't keep previous result
   
   If GetEquipment( TC_PICKED, BusHnd ) = 0 Then 
     BusHnd   = 0
     If 0 = NextBusByName( BusHnd ) Then GoTo HasError
     DoOneBus = 0
   Else
     DoOneBus = 1
   End If

   ' Print report title
   Print #1, "                      BUS FAULT REPORT"
   Print #1, ""
   Print #1, "                                  Phase A      Phase B      Phase C"
   Print #1, ""
   NoFaults = 0
   MaxCurrent = 0
   Do
     ' Prepare outage list at this bus
     NoBranches = 0
     BrHandle    = 0
     While GetBusEquipment( BusHnd, TC_BRANCH, BrHandle ) > 0
       NoBranches = NoBranches + 1
       OutageLst(NoBranches) = BrHandle
     Wend
     OutageLst(NoBranches+1) = 0 ' Close the outage list

     ' Simulate the NoFaults
     If DoFault( BusHnd, FltConn, FltOpt, OutageOpt, OutageLst, _
                 Rflt, Xflt, ClearPrev ) = 0 _
           Then GoTo HasError
     ClearPrev = 0	' Keep result from now on
   Loop Until (DoOneBus = 1 Or NextBusByName( BusHnd ) <= 0)

   FaultFlag = 1	' Show First fault first
   While PickFault( FaultFlag ) <> 0
     If GetSCCurrent( HND_SC, Imagnitude, Iangle, 4 ) = 0 Then GoTo HasError
     FltDescription$ = FaultDescription( )
     Print #1, FltDescription$
     Call OutCurrent( Imagnitude, Iangle )
     Print #1, ""
     For ii = 1 To 3
       If MaxCurrent < Imagnitude(ii) Then
          MaxCurrent = Imagnitude(ii)         ' Store max currrent
          FltMax     = FltDescription   ' Store fault location
       End If
     Next
     FaultFlag = SF_NEXT	' Show next fault
     NoFaults  = NoFaults + 1 ' Update # of imulated NoFaults
   Wend

   Print #1, ""
   Print #1, NoFaults & " Faults Simulated"
   Print #1, ""
   Print #1, "Max fault current = " & Format(MaxCurrent, "#0") & "A found at:"
   Print #1, FltMax$
   Print "Done " & NoFaults & " Faults"
   Stop
   HasError:
   Print "Error: ", ErrorString( )
   Stop
End Sub

Function OpenOutFile() As Long
   ' Open file for output

   ' Dialog data generated by Dialog Edito
   Begin Dialog Dialog_1 49,60, 202, 56, "Output File"
      Text 24,12,56,12, "Enter file name: "
      TextBox 84,12,84,12, .EditBox_1
      OKButton 44,36,52,12
      CancelButton 108,36,48,12
   End Dialog
   Dim dlg As Dialog_1
   Dlg.EditBox_1 = "c:\0tmp\b0.rep"         ' Default name
   ' Dialog returns -1 for OK, 0 for Cancel, button # for PushButtons
   button = Dialog( Dlg )
   If button = 0 Then 
      OpenOutFile = 0
      Exit Function
   End If
   fileName = Dlg.EditBox_1
   Open fileName For Output As #1
   OpenOutFile = 1
End Function

Sub OutCurrent( Mag() As Double, Ang() As Double )
  Print #1,    "                               " ; _
      Format( Mag(1), "####0.0") & "@" & Format( Ang(1), "#0.0"), "     ", _
      Format( Mag(2), "####0.0") & "@" & Format( Ang(2), "#0.0"), "     ", _
      Format( Mag(3), "####0.0") & "@" & Format( Ang(3), "#0.0")
End Sub
