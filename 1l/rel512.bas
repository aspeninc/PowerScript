' ASPEN PowerScript Sample Program
'
' REL512.BAS
'
' Export REL 512 setting data to a text file
'
' Version 1.0
' Category: OneLiner
' 
Sub main()
   Dim ShowRelayFlag (4) As Long
   Dim vDSParams( 40 ) As Double 
   Dim ii As Long
   Dim nCount As Long 

   Const REL512_Z1_Reach = 1
   Const REL512_Z1_Ang   = 2
   Const REL512_Z2_Reach = 5
   Const REL512_Z2_Ang   = 6
   Const REL512_Z2_Dly   = 7
   Const REL512_Z3_Reach = 10
   Const REL512_Z3_Ang   = 11
   Const REL512_Z3_Dly   = 12


   ShowRelayFlag(1) = 1
   ShowRelayFlag(2) = 1
   ShowRelayFlag(3) = 1
   ShowRelayFlag(4) = 1
   ' Get picked device
   If GetEquipment( TC_PICKED, PickedHnd ) = 0 Then GoTo hasError
   ' Probe to see what's being picked
   TypeCode = EquipmentType( PickedHnd )
   If TypeCode <> TC_RLYGROUP Then
     ' Must be a relay group
     Print "Must select a relay group"
     Stop
   End If

   sFName = OpenOutput()
   If sFName = "" Then Stop

   ' Loop through all relays and find their operating times
   RelayCount = 0
   RelayHnd   = 0
   While GetRelay( PickedHnd, RelayHnd ) > 0
     RelayCount = RelayCount + 1
     TypeCode = EquipmentType( RelayHnd )
     If TypeCode = TC_RLYDSG Then
       Call GetData( RelayHnd, DG_sDSType, sDSType$ )
       If sDSType = "REL-512" Then 
         Call GetData( RelayHnd, DG_sID, sID$ )
         Call GetData( RelayHnd, DG_dKmag, dKmag# )
         Call GetData( RelayHnd, DG_dKang, dKang# )
         Call GetData( RelayHnd, DG_dCT, dCT# )
         Call GetData( RelayHnd, DG_dVT, dVT# )
         Call GetData( RelayHnd, DG_vdParams, vDSParams )
         sPrint$ = "DS Relay: " & sID & "(" & sDSType & ")" & Chr(13) & Chr(10) + _
                   "VT_RATIO  = " + Format(dVT,"#0.#") + Chr(13) & Chr(10) + _
                   "CT_RATIO  = " + Format(dCT,"#0.#") + Chr(13) & Chr(10) + _
                   "Z1_K0_MAG = " + Format(dKmag, "#0.#") + Chr(13) & Chr(10) + _
                   "Z1_K0_ANG = " + Format(dKang, "#0.#") + Chr(13) & Chr(10) + _
                   "Z1_REACH  = " + Format(vDSParams(REL512_Z1_Reach), "0.#") + Chr(13) & Chr(10) + _
                   "Z1_LINE_ANG = " + Format(vDSParams(REL512_Z1_Ang), "0.#") + Chr(13) & Chr(10) + _
                   "Z2_K0_MAG = " + Format(dKmag, "0.#") + Chr(13) & Chr(10) + _
                   "Z2_K0_ANG = " + Format(dKang, "0.#") + Chr(13) & Chr(10) + _
                   "Z2_LINE_ANG = " + Format(vDSParams(REL512_Z2_Ang), "0.#") + Chr(13) & Chr(10) + _
                   "Z2_REACH  = " + Format(vDSParams(REL512_Z2_Reach), "0.#") + Chr(13) & Chr(10) + _
                   "Z2_DLY = " + Format(vDSParams(REL512_Z2_Dly), "0.#") + Chr(13) & Chr(10) + _
                   "Z3_K0_MAG = " + Format(dKmag, "0.#") + Chr(13) & Chr(10) + _
                   "Z3_K0_ANG = " + Format(dKang, "0.#") + Chr(13) & Chr(10) + _ 
                   "Z3_LINE_ANG = " + Format(vDSParams(REL512_Z3_Ang), "0.#") + Chr(13) & Chr(10) + _
                   "Z3_REACH  = " + Format(vDSParams(REL512_Z3_Reach), "0.#") + Chr(13) & Chr(10) + _
                   "Z3_DLY = " + Format(vDSParams(REL512_Z3_Dly), "0.#")
'         Print sPrint
         Print #1, sPrint
       End If
     End If
   Wend  'Each relay
   Close #1
   Print "Relays setting have been exported to " + sFName
   Exit Sub
   ' Error handling
   HasError:
   Print "Error: ", ErrorString( )
   Close #1
End Sub  ' End of Sub Main()

' ===================== Dialog box spec (generated by Dialog Editor) ==========
'
'
   Begin Dialog Dialog_1 49,60, 202, 56, "Output File Name"
      Text 24,12,56,12, "Enter file name: "
      TextBox 84,12,84,12, .EditBox_1
      OKButton 44,36,52,12
      CancelButton 108,36,48,12
   End Dialog
'
' ===================== End of Dialog box spec ================================
' ===================== OpenOutput() ==========================================
' Purpose:
'   Open file for output
Function OpenOutput() As String
   Dim dlg As Dialog_1
   Dlg.EditBox_1 = "c:\rel512.txt"         ' Default name
   ' Dialog returns -1 for OK, 0 for Cancel, button # for PushButtons
   button = Dialog( Dlg )
   If button = 0 Then 
     OpenOutput = ""
     Exit Function
   End If
   fileName$ = Dlg.EditBox_1
   Open fileName For Output As #1
   OpenOutput = fileName
End Function
