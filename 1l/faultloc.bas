' ASPEN PowerScript sample program
'
' FAULTLOC.BAS
'
' Find location of a fault on a line. First simulate an intermediate 
' fault every 1 %. Then  compare result to recorded current values.
' 
' Version 1.0
' Category: OneLiner
'
' PowerScript functions called:
'   DoFault()
'   PickFault()
'   EquipmentType()
'   GetSCCurrent()
'   FaultDescription()
'
' Main program code
Sub main()
   ' Variable declaration
   Dim ShowFlagRly(4) As Long
   Dim MagArray(16) As Double
   Dim AngArray(16) As Double
   Dim FltConnection(4) As Long
   Dim FltOption(14) As Double
   Dim OutageType(3) As Long
   Dim OutageList(15) As Long
   Dim Ia As Double, Ib As Double, Ic As Double, StepSize As Double
   Dim MinErr As Double, ErrVal As Double
   Dim PickedHnd As Long, BranchHnd As Long, DoneFlag As Long, ShowFaultFlag As Long
   Dim FaultString As String

   ' Initialize 
   For ii = 1 To 4 
     ShowFlagRly(ii) = 1
   Next 
   For ii = 1 To 4 
     FltConnection(ii) = 0
   Next 
   For ii = 1 To 14
     FltOption(ii) = 0.0
   Next
   For ii = 1 To 3
     OutageType(ii) = 0
   Next
   dFltR     = 0.0   '
   dFltX     = 0.0
   ClearPrev = 1 ' Don't keep previous result

   If GetEquipment( TC_PICKED, PickedHnd ) = 0 Then GoTo hasError
   ' Must be a relay group
   If EquipmentType( PickedHnd ) <> TC_RLYGROUP Then
     Print "Must select a relay group"
     Exit Sub
   End If
   If InputDialog( FltConnection, Ia, Ib, Ic ) = 0 Then Stop
   ' Simulate fault at this bus
   StepSize = 1  ' Every 1%
   DoneFlag = 0
   MinErr   = 1e12
   FltOption(9)  = StepSize
   FltOption(13) = 0  ' Start from 0% 
   FltOption(14) = 100  ' To 100% 
   If GetData( PickedHnd, RG_nBranchHnd, BranchHnd ) = 0 Then GoTo HasError
   ' Simulate intermediate faults
   If 0 = DoFault( PickedHnd, FltConnection, FltOption, OutageType, OutageList, _
                   dFltR, dFltX, ClearPrev ) Then GoTo HasError
   ' Check fault result and find the best match
   ShowFaultFlag = 1 ' Starting from the first one
   ' Pick fault does not update single line diagram screen like ShowFault
'   While PickFault( ShowFaultFlag ) > 0
   While ShowFault( ShowFaultFlag, 1, 1, 0, ShowFlagRly ) > 0
    ' Get branch current
    If GetSCCurrent( BranchHnd, MagArray, AngArray, 4 ) = 0 Then GoTo HasError
    ' Compute squared error
    ErrVal = (MagArray(1) - Ia)*(MagArray(1) - Ia) + _
            (MagArray(2) - Ib)*(MagArray(2) - Ib) + _
            (MagArray(3) - Ic)*(MagArray(3) - Ic)
    If ErrVal < MinErr Then
      ' Update the minimum
      MinErr      = ErrVal
      FaultString = FaultDescription()
    End If
    ShowFaultFlag = SF_NEXT   ' Show next fault
   Wend

   ' Print result
   Print "Least error = "; Format( MinErr, "0" ); " Found at: "; Chr(10); FaultString
   Exit Sub
HasError:
   Print "Error: ", ErrorString( )
End Sub  ' End of Sub Main()
' ===================== End of Main() =========================================


' ===================== Dialog box spec (generated by Dialog Editor) ==========
'
'
Begin Dialog FAULTDLG 48,46, 133, 119, "Locate a fault"
  Text 8,8,96,8, "Simulate Fault Connections"
  Text 8,32,104,8, "Recorded phase current: (A)"
  CheckBox 16,16,28,8, "3PH", .CheckBox_1
  CheckBox 44,16,28,8, "2LG", .CheckBox_2
  CheckBox 72,16,28,8, "1LG", .CheckBox_3
  CheckBox 100,16,24,8, "LL", .CheckBox_4
  Text 16,48,36,8, "Phase A ="
  TextBox 52,44,40,12, .EditBox_1
  TextBox 52,60,40,12, .EditBox_2
  Text 16,64,36,8, "Phase B ="
  TextBox 52,76,40,12, .EditBox_3
  Text 16,80,36,8, "Phase C ="
  OKButton 12,96,48,12
  CancelButton 68,96,48,12
End Dialog
'
' ===================== End of Dialog box spec ================================
'
' ===================== InputDialog() =========================================
' Purpose:
'   Get Fault spec. inputs from user
'
Function InputDialog( FltConnection() As Long, ByRef Ia As Double, _
      ByRef Ib As Double, ByRef Ic As Double ) As Long
  Dim dlg As FAULTDLG
  ' Check all fault connection
  dlg.CheckBox_1 = 1
  dlg.CheckBox_2 = 1
  dlg.CheckBox_3 = 1
  dlg.CheckBox_4 = 1
  dlg.EditBox_1  = 2967
  dlg.EditBox_2  = 37
  dlg.EditBox_3  = 20
  DoneFlag = 0
  While DoneFlag = 0
    button = Dialog( dlg )
    If button = 0 Then ' Canceled
      InputDialog = 0
      Exit Function
    End If
    FltConnection(1) = dlg.CheckBox_1
    FltConnection(2) = dlg.CheckBox_2
    FltConnection(3) = dlg.CheckBox_3
    FltConnection(4) = dlg.CheckBox_4
    Ia = Val(dlg.EditBox_1)
    Ib = Val(dlg.EditBox_2)
    Ic = Val(dlg.EditBox_3)
    If (Ia > 0 Or Ib > 0 Or Ic > 0) Then 
       If FltConnection(1)=1 Or FltConnection(2)=1 Or FltConnection(3)=1 _
           Or FltConnection(4)=1 Then 
         DoneFlag = 1
       Else
         Print "Must select a fault connection"
       End If
    Else
      Print "Must input fault current"
    End If 
  Wend
  InputDialog = 1
End Function
' ===================== End of InputDialog() ==================================
