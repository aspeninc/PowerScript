' ASPEN PowerScript Sample Program
'
' LINEIMP.BAS
'
' Produce line impedance report
'
' Demonstrate how to generate a customized report from a PowerScript 
' program
'
' PowerScript functions called:
'   GetData()
'   FullBusName()
'   GetEquipment()
'
Sub main()
   ' Open output file
   If OpenOutput() = 0 Then Exit Sub
   ' Loop thru all lines
   DevHandle = 0
   LineID    = ""
   Bus1ID    = ""
   Bus2ID    = ""
   Counts    = 0
   While GetEquipment( TC_LINE, DevHandle ) > 0
     ' Get Line ID and end bus names
     If GetData( DevHandle , LN_sID,   LineID ) = 0 Then GoTo HasError
     If GetData( DevHandle , LN_nBus1Hnd, Bus1Handle ) = 0 Then GoTo HasError
     If GetData( DevHandle , LN_nBus2Hnd, Bus2Handle ) = 0 Then GoTo HasError
     ' Get impedances
     If GetData( DevHandle, LN_dR, R ) = 0 Then GoTo HasError
     If GetData( DevHandle, LN_dX, X ) = 0 Then GoTo HasError
     If GetData( DevHandle, LN_dR0, R0# ) = 0 Then GoTo HasError
     If GetData( DevHandle, LN_dX0, X0# ) = 0 Then GoTo HasError
     If R0 =0.0 And X0 < 0.0001 Then Flag$ = " Z0=0" Else Flag$ = ""
     Print #1, _
        FullBusName( Bus1Handle ) & " - "; FullBusName( Bus2Handle) & " " & LineID _
        & "  " & Format( R,  "    #.0000  " ) & Format( X,  "    #.0000  " ) _
        & "  " & Format( R0, "    #.0000  " ) & Format( X0, "    #.0000  " ) & Flag$

     Counts = Counts + 1
   Wend
   Print Counts; " Lines Exported"
   Exit Sub
HasError:
   Print "Error: ", ErrorString( )
End Sub

' ===================== Dialog box spec (generated by Dialog Editor) ==========
'
'
   Begin Dialog Dialog_1 49,60, 202, 56, "Output File Name"
      Text 24,12,56,12, "Enter file name: "
      TextBox 84,12,84,12, .EditBox_1
      OKButton 44,36,52,12
      CancelButton 108,36,48,12
   End Dialog
'
' ===================== End of Dialog box spec ================================
' ===================== OpenOutput() ==========================================
' Purpose:
'   Open file for output
Function OpenOutput() As Long
   Dim dlg As Dialog_1
   Dlg.EditBox_1 = "c:\0tmp\lineimp.rep"         ' Default name
   ' Dialog returns -1 for OK, 0 for Cancel, button # for PushButtons
   button = Dialog( Dlg )
   If button = 0 Then 
     OpenOutput = 0
     Exit Function
   End If
   fileName = Dlg.EditBox_1
   Open fileName For Output As #1
   OpenOutput = 1
End Function